<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" backgroundColor="0xffffff"
	 creationComplete="complete()" paddingLeft="3"
	 styleName="MyFont" fontSize="11">
	<mx:Label text="Login in with your google account" fontSize="13"/>
	<mx:Spacer height="5"/>
	<mx:HBox>
		<mx:Label text="Email:" width="{labelPasswd.width}"/>
		<mx:ComboBox id="email" editable="true" cornerRadius="0" paddingLeft="0" paddingRight="0" fontWeight="normal"
			 change="onEmailChanged()"/>		
	</mx:HBox>
	<mx:HBox>
		<mx:Label id="labelPasswd" text="Password:"/>
		<mx:TextInput id='passwd' displayAsPassword="true"/>
	</mx:HBox>	
	<mx:CheckBox label="Remember my password" id="rememberPasswd"/>
	<mx:CheckBox label="Sign in automaticlly" id="autoSignIn"/>	
	<mx:Spacer height="10"/>
	
	<mx:Canvas width="100%">	
	<mx:Button label="Sign In" horizontalCenter="0" click="SignIn()"/>	
	</mx:Canvas>
	<mx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			include 'engine.as'
			
			private var _configDoc:XML;
			
			private static const CONFIG_FILE_NAME:String = 'config.xml';
			
			private var configFile:File;
			
			private function loadConfig():void {				
				var fs:FileStream = new FileStream;				
				if(!configFile.exists) {
					_configDoc = <config></config>;
					fs.open(configFile, FileMode.WRITE);
					fs.writeUTFBytes(_configDoc.toXMLString());
				} else {
					fs.open(configFile, FileMode.READ);
					_configDoc = new XML(fs.readUTFBytes(fs.bytesAvailable));
				}
				fs.close();
			}
			
			private function putConfig():void {
				var fs:FileStream = new FileStream;
				fs.open(configFile, FileMode.WRITE);
				fs.writeUTFBytes(_configDoc.toXMLString());
				fs.close();
			}
			
			private function onEmailChanged():void {
				if( rememberPasswd.selected ) {
					passwd.text = email.selectedItem.passwd;
				} else {
					passwd.text = '';
				}
			}
			
			private function complete():void {
				//configFile = File.applicationDirectory.resolvePath(CONFIG_FILE_NAME);
				configFile = new File(File.applicationDirectory.resolvePath(CONFIG_FILE_NAME).nativePath);
				trace(configFile.nativePath);
				loadConfig();
				var dp:ArrayCollection = new ArrayCollection;
				var users:XMLList = _configDoc.user;
				var lastSignInUser:XML;
				var selectedItem:Object;
				for each(var user:XML in users) {
					var d:Object = {label:user.email, passwd:user.password};					
					dp.addItem(d);
					if( user.lastSignIn == 'true' ) {
						lastSignInUser = user;
						selectedItem = d;
					}
				}
				email.dataProvider = dp;				
				if( lastSignInUser ) {
					email.selectedItem = selectedItem;
					passwd.text = lastSignInUser.password;
					rememberPasswd.selected = passwd.text == '' ? false:true;
				}
			}
			
			private function storeCurrentUserInfo(currentUser:Account):void {
				var users:XMLList = _configDoc.user;
				var newUser:Boolean = true;
				for each( var user:XML in users) {
					if( user.email == currentUser.email ) {
						newUser = false;
						user.password = currentUser.passwd;
						user.lastSignIn = 'true';
						
					} else {
						user.lastSignIn = 'false';
					}
				}
				if( newUser ) {
					var userNode:XML = <user></user>;
					userNode.email = currentUser.email;
					userNode.password = currentUser.passwd;
					userNode.lastSignIn = 'true';
					_configDoc.appendChild(userNode);
				}
				putConfig(); 
			}
			
			private function SignIn():void {
				var url:String = 'https://www.google.com/accounts/ClientLogin';
				var vars:URLVariables = new URLVariables;
				vars.accountType = 'GOOGLE';
				vars.Email = email.text;
				vars.Passwd = passwd.text;
				vars.service = 'xapi';
				vars.source = 'Frank-xtasks-0.01';
				
				var resultHandler:Function = function(event:Event):void {
					var loader:URLLoader = event.target as URLLoader;
					trace(loader.data);
				}
				
				var httpResponseHandler:Function = function(event:HTTPStatusEvent):void {
					var statusCode:int = event.status;
					if( statusCode == 200 ) {
						trace('login sucessfully');
						var account:Account = new Account;
						account.email = vars.Email;
						if( rememberPasswd.selected ) {
							account.passwd = vars.Passwd;
						}
						storeCurrentUserInfo(account);
						data.onLogin(account);
					}
					else {
						trace('login failed, status code is', event.status);
						
					}					
				}
				send1(url, resultHandler, httpResponseHandler, URLRequestMethod.POST, vars);
			}
			
		]]>
	</mx:Script>
	
	
</mx:VBox>
