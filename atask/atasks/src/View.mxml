<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" creationComplete="complete()"
	xmlns:local="*">
	
	<mx:HBox width="100%">		
		<mx:Button label="*" click="list()"/>
		<mx:Button label="+" click="onBtnAdd()"/>
	</mx:HBox>
	
	<mx:Canvas width="100%" height="100%">
		<local:MyList id="listTasks" itemRenderer="ItemTask" width="100%" height="100%" change="onChanged()"/>	
	</mx:Canvas>	
		
	<mx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			include 'engine.as'
			
			private var siteURL:String = 'http://xatasks.appspot.com/';
			
			private function list():void {
				var url:String = siteURL + 'list';
				var resultHandler:Function = function(event:Event):void {
					var xml:String = URLLoader(event.target).data;
					trace(xml);
					var xmlDoc:XML = new XML(xml);
					var tasks:XMLList = xmlDoc.task;
					var dp:ArrayCollection = new ArrayCollection;					
					for each(var task:XML in tasks) {
						var t:Task = new Task;
						t.key = task.@key;
						t.content = task.@content;
						addItem(t,dp);
					}									
					listTasks.dataProvider = dp;
				}
				send(url, resultHandler);
			}
			
			private function complete():void {
				var t:Timer = new Timer(1000,1);
				t.addEventListener(TimerEvent.TIMER_COMPLETE, function(event:TimerEvent):void {
					list();
				},false,0,true);
				t.start();
			}
			
			private function onChanged():void {
				trace('onChanged');				
			}
			
			private function addItem(task:Task, dp:ArrayCollection):void {				
				var item:Object = new Object;
				item.task = task;
				item.fnDeleteTask = deleteTask;
				item.fnUpdateTask = updateTask;
				dp.addItem(item);
			}
			
			private function onBtnAdd():void {
				trace('onBtnAdd');							
				var url:String = siteURL + 'add';
				var vars:URLVariables = new URLVariables;
				vars.content = '';
				var resultHandler:Function = function(event:Event):void {
					var key:String = URLLoader(event.target).data.key;					
					var t:Task = new Task;
					t.key = key;
					var dp:ArrayCollection = listTasks.dataProvider as ArrayCollection;	
					addItem(t,dp);					
				}
				send(url,resultHandler, URLRequestMethod.POST, vars, URLLoaderDataFormat.VARIABLES);				
			}
			
			
			
			private function deleteTask(item:Object):void {
				var task:Task = item.task;				
				var url:String = siteURL + 'delete';
				var vars:URLVariables = new URLVariables;
				vars.key = task.key;
				var resultHandler:Function = function(event:Event):void {
					trace(URLLoader(event.target).data);
					var dp:ArrayCollection = listTasks.dataProvider as ArrayCollection;
					var index:int = dp.getItemIndex(item);
					dp.removeItemAt(index);
				}
				send(url, resultHandler, URLRequestMethod.POST, vars);
			}
			
			private function updateTask(task:Task):void {
				var url:String = siteURL + 'update'
				var vars:URLVariables = new URLVariables;
				vars.key = task.key;
				vars.content = task.content;
				var resultHandler:Function = function(event:Event):void {
					trace(URLLoader(event.target).data);
				}
				send(url, resultHandler, URLRequestMethod.POST, vars);
			}
			
			
		]]>
	</mx:Script>	
</mx:VBox>
